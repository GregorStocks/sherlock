@ref "bin/IronPython.dll"
@ref "bin/Microsoft.Scripting.dll"
@ref "bin/Microsoft.Scripting.Core.dll"

use IronPython
use Microsoft.Scripting
use Microsoft.Scripting.Hosting
use System.Security
use System.Security.Permissions
use System.Security.Policy

class ScriptEnv
	var _engine as ScriptEngine
	var _scope as ScriptScope
	cue init
		.init('scripts')

	cue init(scriptDir as String)
		"""A script. Can only run files in the scriptDir."""
		base.init
		setup = AppDomainSetup()
		setup.applicationBase = AppDomain.currentDomain.baseDirectory
		setup.applicationName = 'Testing'

		evidence = Evidence()
		evidence.addHost(Zone(SecurityZone.Internet))

		perms = PermissionSet(PermissionState.None)
		perms.addPermission(SecurityPermission(SecurityPermissionFlag(Execution, _
		                                                              SerializationFormatter)))
		perms.addPermission(FileIOPermission(FileIOPermissionAccess(Read, PathDiscovery), _
		                                     Path.getFullPath(scriptDir)))

		sandbox = AppDomain.createDomain('Sandbox', evidence, setup, perms)

		_engine = IronPython.Hosting.Python.createEngine(sandbox) to !
		_scope = _engine.createScope to !

		runtime = _engine.runtime
		runtime.loadAssembly(String.getType.assembly)
		runtime.loadAssembly(Uri.getType.assembly)

		.eval<of Object>("import sys; sys.path.append('[scriptDir]')")
	
	class Five has Serializable
		def getFive as int
			return 5

	def importModule(name as String)
		"""Imports the given module"""
		test
			script = ScriptEnv('test/scripts')
			script.importModule('nop')
			# expect SecurityException, script.importModule('../base')
			script.setVariable('c', Five())
			assert script.eval<of int>("c.GetFive()") == 5
		body
			.eval("import [name]")

	def setVariable(name as String, object as Object)
		"""
		Adds the given object to the script's scope with the given name. Remember that Cobra
		internally mangles all names from lowerCamelCase to UpperCamelCase, so all the members of
		the object will be named differently in Python than in Cobra.
		"""
		_scope.setVariable(name, object)

	def getVariable<of T>(name as String) as T
		"""Returns the variable with the given name in the script's scope."""
		return _scope.getVariable<of T>(name)

	def eval<of T>(cmd as String) as T
		try
			return _engine.execute<of T>(cmd, _scope)
		catch e as SyntaxErrorException
			print "Error at [e.line],[e.column] of [e.sourcePath]"
			print "Source was:\n[e.sourceCode]"
			throw e

	def eval(cmd as String)
		.eval<of Object>(cmd)

