@ref "bin/Lidgren.Network.dll"
@ref "bin/sfmlnet-graphics.dll"
@ref "bin/sfmlnet-window.dll"

use Lidgren.Network
use SFML.Window
use SFML.Graphics

class SherlockGame
	var _client as NetClient
	var _window as RenderWindow?
	var _input as Input?
	var _state as ClientState
	var _view as View?

	cue init(client as NetClient)
		base.init
		_client = client
		_state = ClientState(client)
	
	def move(dx as int, dy as int)
		for id, e in _state.entities
			if id == _state.id
				p = Point(e.pos.x + dx, e.pos.y + dy)
				msg = Message.writeToWire(MoveMessage(id, e.pos, p), _client)
				_client.sendMessage(msg, NetDeliveryMethod.ReliableOrdered)

	def handleInput
		# Input that we want to handle per-frame rather than as a series of events - mouse position and arrow keys.
		if _input.isKeyDown(KeyCode.Left)
			.move(-1, 0)
		if _input.isKeyDown(KeyCode.Right)
			.move(1, 0)
		if _input.isKeyDown(KeyCode.Up)
			.move(0, -1)
		if _input.isKeyDown(KeyCode.Down)
			.move(0, 1)
	
	def handleMessages
		while true
			msg = _client.readMessage
			if not msg
				break
			if msg.messageType == NetIncomingMessageType.Data
				m = Message.readCommandFromWire(msg to !)
				m.execute(msg.senderConnection to !, _state)
			_client.recycle(msg)
	
	def draw
		_window.clear
		for id, e in _state.entities
			s = _state.sprites[id]
			s.position = Vector2(e.pos.x * 32, e.pos.y * 32)
			_window.draw(s)
			if id == _state.id
				_view.center = Vector2(e.pos.x * 32, e.pos.y * 32)
	
	def onClosed(o as Object, args as EventArgs)
		_window.close
	
	def onKeyPressed(o as Object, args as KeyEventArgs)
		pass

	def onResized(o as Object, args as SizeEventArgs)
		_view.halfSize = Vector2(args.width / 2f32, args.height / 2f32)
	
	def run
		_window = RenderWindow(VideoMode(800, 600, 32), "SFML Graphics")
		_window.setFramerateLimit(60)
		_window.clear
		_view = View(Vector2(0, 0), Vector2(400, 300))
		_window.currentView = _view
		_input = _window.input
		listen _window.closed, EventHandler(ref .onClosed)
		listen _window.keyPressed, EventHandler<of KeyEventArgs>(ref .onKeyPressed)
		listen _window.resized, EventHandler<of SizeEventArgs>(ref .onResized)
		while _window.isOpened
			.handleInput
			_window.dispatchEvents
			.handleMessages
			.draw
			_window.display

class SherlockClient
	def main
		config = NetPeerConfiguration("MyExampleName")
		client = NetClient(config)
		client.start
		client.connect('localhost', 14242)
		msg = Message.writeToWire(AddMessage(Entity()), client)
		client.sendMessage(msg, NetDeliveryMethod.ReliableOrdered)
		SherlockGame(client).run
