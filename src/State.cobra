use Lidgren.Network

class State inherits MarshalByRefObject is abstract
	# ID -> Entity mapping
	pro entities from var as IDictionary<of int, Entity>

	cue init is protected
		base.init
		.entities = Dictionary<of int, Entity>()
	
	def addEntity(e as Entity)
		.entities.add(e.id, e)
	
	def moveEntity(id as int, src as Point?, dest as Point)
		.entities[id].pos = dest

class ServerState inherits State
	# IDs are positive integers. An ID of 0 represents "no ID."
	# Thread-unsafe.
	shared
		var _idCounter as int = 0
	def getNewId as int
		_idCounter += 1
		return _idCounter
	var _server as NetServer
	# entity ID -> Connection
	var _connections = Dictionary<of int, NetConnection>()
	var _scriptEnv as ScriptEnv

	cue init(server as NetServer)
		base.init
		_scriptEnv = ScriptEnv()
		_server = server
		_scriptEnv.setVariable('ServerState', this)
		_scriptEnv.executeFile('scripts/map.py')
		_scriptEnv.eval('map(ServerState)')
		_scriptEnv.executeFile('scripts/ai.py')

	def addEntity(e as Entity, c as NetConnection)
		if e.id == 0
			e.id = .getNewId

		_connections.add(e.id, c)

		# tell new guy about everyone
		for entity in .entities.values
			if Math.abs(entity.pos.x - e.pos.x) < 5 and Math.abs(entity.pos.y - e.pos.y) < 5
				msg = Message.writeToWire(AddMessage(entity), _server)
				c.sendMessage(msg, NetDeliveryMethod.ReliableOrdered)

		base.addEntity(e)

		# tell everyone about new guy
		for i, c in _connections
			print i
			entity = .entities[i]
			if Math.abs(entity.pos.x - e.pos.x) < 5 and Math.abs(entity.pos.y - e.pos.y) < 5
				msg = Message.writeToWire(AddMessage(e), _server)
				_server.sendMessage(msg, c, NetDeliveryMethod.ReliableOrdered, 0)

		# tell new guy who he is
		msg = Message.writeToWire(IdMessage(e.id), _server)
		c.sendMessage(msg, NetDeliveryMethod.ReliableOrdered)
	
	def moveEntity(id as int, src as Point?, dest as Point) is override
		base.moveEntity(id, src, dest)
		e = .entities[id]
		for i, c in _connections
			entity = .entities[i]
			if Math.abs(entity.pos.x - e.pos.x) < 5 and Math.abs(entity.pos.y - e.pos.y) < 5
				msg = Message.writeToWire(MoveMessage(id, src, dest), _server)
				_server.sendMessage(msg, c, NetDeliveryMethod.ReliableOrdered, 0)
	
	def tick
		for id, e in .entities
			if not _connections.containsKey(id)
				# No player, so AI it up!
				dx = _scriptEnv.eval<of int>('getMoveX()')
				dy = _scriptEnv.eval<of int>('getMoveY()')
				.moveEntity(id, e.pos, Point(e.pos.x + dx, e.pos.y + dy))
	
	def makeEntity(x as int, y as int, icon as String)
		.addEntity(Entity(Point(x, y), .getNewId, icon))
